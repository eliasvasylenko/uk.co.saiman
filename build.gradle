/*
 * Master Gradle build script
 *
 * Depends on bndPlugin property set by settings.gradle.
 * and bnd_* values from gradle.properties.
 */

/* Add bnd gradle plugin as a script dependency */
buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath 'gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.13.1'
    classpath ('org.ajoberstar:gradle-git:1.5.1') {
      /* Resolve dependency conflict with bndtools: */
      exclude group: 'org.apache.httpcomponents'
    }
  }
}

wrapper {
  jarFile = rootProject.file('.gradle-wrapper/gradle-wrapper.jar')
  gradleVersion = '6.6'
}

/* Configure the subprojects */
def bndProjects() {
  return subprojects.findAll {
    it.plugins.hasPlugin('biz.aQute.bnd')
  }
}

/* Test Configuration */
configure(bndProjects()) {
  apply plugin: 'jacoco'

  jacoco {
    toolVersion = "0.8.2"
  }

  repositories {
    mavenCentral()
  }

  jacocoTestReport {
    reports {
      xml.enabled true
      html.enabled false
    }
  }
}

/* Source License Generation */
def copyrightProperties  = new Properties()
rootProject.file('copyright.properties').withReader { reader ->
  copyrightProperties.load(reader)
  println copyrightProperties.'copyright.holder.logo'
}
apply plugin: 'license'
configure(bndProjects()) {
  apply plugin: 'license'

  license {
    header rootProject.file('LICENSE.TEMPLATE')

    ext.'copyright.work.name' = project.name
    ext.'copyright.year' = Calendar.getInstance().get(Calendar.YEAR)
    ext.'copyright.holder.name' = copyrightProperties.'copyright.holder.name'
    ext.'copyright.holder.email' = copyrightProperties.'copyright.holder.email'
    ext.'copyright.holder.logo' = copyrightProperties.'copyright.holder.logo'

    strictCheck true

    mapping {
      java = 'SLASHSTAR_STYLE'
    }
  }
}

/* Javadoc Generation */
def javadocOptions(options, title) {
  configure(options) {
    docTitle = title
    windowTitle = title
    memberLevel = JavadocMemberLevel.PROTECTED
    charSet = 'UTF-8'
    encoding = 'UTF-8'
    docEncoding = 'UTF-8'
    links('http://docs.oracle.com/javase/8/docs/api/')
  }
}

def javadocExports(project) {
  def javadocSpecs = project.bnd('Export-Package', project.name)

  return javadocSpecs.split(/\s*,\s*/).collect {
    it.replace('.','/')+"/*.java"
  }
}

configure(bndProjects()) {
  def javadocTitle = bnd('javadoc.title', project.name)

  javadoc {
    javadocOptions(options, javadocTitle)

    javadocExports(project).each { include it }

    doFirst {
      project.delete(destinationDir)
      logger.info "Title    : ${options.windowTitle}"
      logger.info "Destdir  : ${destinationDir}"
    }
  }
}

task aggregateJavadoc(type: Javadoc) {
  javadocOptions(options, project.name)

  bndProjects().each {
    javadocExports(it).each { include it }
  }

  source bndProjects().collect { it.sourceSets.main.allJava }

  destinationDir = new File(buildDir, 'javadoc')

  classpath = files(bndProjects().collect {project -> project.sourceSets.main.compileClasspath})

  doFirst {
    project.delete(destinationDir)
    logger.info "Title    : ${options.windowTitle}"
    logger.info "Destdir  : ${destinationDir}"
  }
}

/* Javadoc Publish to GH Pages */
apply plugin: 'org.ajoberstar.github-pages'

githubPages {
  repoUri = 'https://github.com/Scientific-Analysis-Instruments/uk.co.saiman.git'
  if (project.hasProperty('githubUser') && project.hasProperty('githubPassword')) {
    credentials.setUsername githubUser
    credentials.setPassword githubPassword
  }

  pages {
    from aggregateJavadoc
  }
}
